buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url = 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath group: 'com.wynprice.cursemaven', name: 'CurseMaven', version: '2.1.1'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.wynprice.cursemaven'

ext.configFile = file('gradle.properties')
ext.config = parseConfig(configFile)

version = "${minecraft_version}-${mod_version}"
group = "svenhjol.charm"
archivesBaseName = "${mod_name}"

minecraft {
    coreMod = "svenhjol.charm.base.CharmLoadingPlugin"
    version = "1.12.2-14.23.5.2847"
    mappings = "stable_39"
    runDir = 'run'
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${mod_name}",
                "Specification-Vendor": "svenhjol",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": "${mod_name}",
                "Implementation-Version": "${mod_version}",
                "Implementation-Vendor": "svenhjol",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLCorePluginContainsFMLMod": "true",
                "FMLCorePlugin": "svenhjol.charm.base.CharmLoadingPlugin"
        ])
    }
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    maven { // for AutoRegLib and Quark
        name "blamejared"
        url "http://maven.blamejared.com/"
    }
    maven { // JEI & Tinkers
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven { // for HWYLA
        name 'Mobius'
        url "https://maven.tehnut.info"
    }
    maven {
        name = 'CurseForge'
        url = 'https://minecraft.curseforge.com/api/maven/'
    }
    flatDir {
        // for including local jar files
        dirs "libs"
    }
}

dependencies {
//    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // set to "compileOnly" to test without quark
    compileOnly "curse.maven:${arl}"
    compileOnly "curse.maven:${quark}"
    compileOnly "mezz.jei:jei_${minecraft_version}:${jei}:api"
    runtimeOnly "mezz.jei:jei_${minecraft_version}:${jei}"
//    compile "inventory-tweaks:InventoryTweaks:${invtweaks}:api"
//    deobfProvided "curse:maven:${quark}:api"
//    runtime "mcp.mobius.waila:Hwyla:${waila}_${minecraft_version}"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': minecraft_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', '**/psd/**'
    }
}

static def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

defaultTasks 'clean', 'build'